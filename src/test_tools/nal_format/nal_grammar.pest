//! ã€Œç»Ÿä¸€`.nal`æ ¼å¼ã€è¯­æ³•
//! * ğŸ¯ä»å¸¸è§çš„`.nal`æ–‡ä»¶ä¸­è§£æå‡ºã€ŒNAVMæµ‹è¯•è¯­å¥ã€
//! * ğŸ¯åœ¨ã€ŒASCII CommonNarseseã€ä¹‹ä¸‹ï¼Œé™„åŠ æ³¨é‡Šä¸ã€Œæµ‹è¯•é¢„æœŸã€è¯­æ³•
//! * ğŸ“åŸåˆ™ï¼šå‡¡æ˜¯æ— æ³•å¯¹åº”åˆ°ã€Œè§£æç»“æœã€ï¼ˆæ­¤å¤„æ˜¯NALInputç»“æ„ï¼‰çš„ï¼Œéƒ½ç»™ã€Œé™é»˜ã€unwrap

/// ç©ºç™½ç¬¦ | æ‰€æœ‰éæ¢è¡Œçš„Unicodeç©ºç™½ç¬¦ï¼Œè§£æå‰å¿½ç•¥
/// * ğŸš©ã€2024-03-31 23:40:41ã€‘ç°åœ¨å°†ã€Œè¡Œåˆ†å‰²ã€äº¤ç»™Rusté¢„å¤„ç†
/// * ğŸ“Œæ­¤å¤„å¯¹ã€Œæ¢è¡Œç¬¦ã€çš„ç‰¹æ®Šå¤„ç†ï¼ŒåŸºæœ¬ä¸ä¼šç”¨åˆ°
WHITESPACE = _{ !"\n" ~ WHITE_SPACE }

/// å¤šä¸ª`.nal`è¾“å…¥
/// * ğŸš©ã€2024-03-31 23:41:33ã€‘ç›®å‰ä¸å¯ç”¨ï¼Œå°†ã€Œè¡Œåˆ†å‰²ã€äº¤ç»™Rusté¢„å¤„ç†
nal_inputs = !{
    "\n"* ~ nal_input ~ ("\n"+ ~ nal_input)*
}

/// å…¥å£/å•ä¸ª`.nal`è¾“å…¥ï¼ˆé™é»˜å±•å¼€ï¼‰
/// * ğŸš©ä»¥æ•°å­—ä»£æ›¿`CYC`æŒ‡ä»¤ï¼Œå¹¶å…¼å®¹åŸ`.nal`è¯­æ³•
/// * ğŸš©ä»¥ç›´æ¥çš„Narseseä»£æ›¿`NSE`æŒ‡ä»¤ï¼Œå¹¶å…¼å®¹åŸ`.nal`è¯­æ³•
/// * ğŸš©åœ¨æ³¨é‡Šä¸­æ‰©å±•æ–°è¯­æ³•
nal_input = _{
    (cyc_uint | comment | narsese)
}

/// ç›´æ¥å¯¹åº”CYCæŒ‡ä»¤çš„ç”¨æ³•
/// * âœ…`CYC`çš„è¯­æ³•ç³–
cyc_uint = { ASCII_DIGIT+ }

/// æ³¨é‡Šï¼ˆé™é»˜ï¼‰
/// * ğŸš©åŒ…æ‹¬ã€Œè¾“å‡ºé¢„æœŸã€ç­‰ã€Œé­”æ³•æ³¨é‡Šã€
comment = _{
    comment_head ~ (comment_navm_cmd | comment_sleep | comment_await | comment_expect_contains | comment_save_outputs | comment_terminate | comment_raw)
}

/// æ³¨é‡Šçš„å¤´éƒ¨å­—ç¬¦ï¼ˆé™é»˜ï¼‰
comment_head = _{ "'" }

/// æœ‰å…³ã€Œç½®å…¥å‘½ä»¤ã€çš„ã€Œé­”æ³•æ³¨é‡Šã€
/// * âœ¨å…è®¸æ„å»ºå¹¶å‘NAVMç½®å…¥æŒ‡ä»¤
/// * ğŸ“„ç”¨`'/VOL 0`ä»£æ›¿éé€šç”¨çš„`*volume=0`
/// * ğŸ“„äº¦å¯ç›´æ¥ç”¨ä¸‰ä¸ª`'`ï¼š`'''VOL 0`
/// * ğŸš©ã€2024-04-02 23:44:06ã€‘ç°åœ¨ä½¿ç”¨`$`è¦æ±‚ã€Œç‰¹æ®Šå‰ç¼€ã€ç´§æŒ¨å†…å®¹ï¼Œé¿å…è¯¯è®¤ã€Œè¢«æ³¨é‡Šæ‰çš„æ³¨é‡Šã€ä¸ºæŒ‡ä»¤
/// * âœ…ä½¿ç”¨æ­£è°“è¯`&LETTER`è¦æ±‚å¿…é¡»æ˜¯`/æ–‡å­—`è€Œéå…¶å®ƒï¼ˆå¦‚`//`ï¼‰
comment_navm_cmd = ${
    // ç‰¹æ®Šå‰ç¼€`/`æˆ–`''`
    ("/" | "''") ~ &LETTER ~ comment_raw
}

/// æœ‰å…³ã€Œç¡çœ ç­‰å¾…ã€çš„ã€Œé­”æ³•æ³¨é‡Šã€
/// * âœ¨å…è®¸æ„å»ºå¹¶å‘NAVMç½®å…¥æŒ‡ä»¤
/// * ğŸ“„ç”¨`'/VOL 0`ä»£æ›¿éé€šç”¨çš„`*volume=0`
/// * å…·ä½“çš„ã€Œæ—¶é—´æ ¼å¼ã€ç•™ç»™Rustä¾§
comment_sleep = !{
    // é¢å¤–çš„å‰ç¼€
    "'sleep:" ~ WHITESPACE* ~ comment_raw
}

/// æœ‰å…³ã€Œè¾“å‡ºç­‰å¾…ã€çš„ã€Œé­”æ³•æ³¨é‡Šã€
/// âœ¨é˜»å¡ä¸»çº¿ç¨‹ï¼Œç­‰å¾…NAVMçš„æŸä¸ªè¾“å‡ºå†ç»§ç»­
comment_await = {
    // é¢å¤–çš„å‰ç¼€
    "'await:" ~ output_expectation
}

/// æœ‰å…³ã€Œè¾“å‡ºé¢„æœŸï¼ˆåŒ…å«ï¼‰ã€çš„ã€Œé­”æ³•æ³¨é‡Šã€
/// âœ¨æ£€æŸ¥NAVMçš„æ‰€æœ‰è¾“å‡ºï¼Œè¿”å›ã€Œæ˜¯å¦æœ‰ç¬¦åˆé¢„æœŸçš„è¾“å‡ºã€çš„[`Result`]
comment_expect_contains = {
    // é¢å¤–çš„å‰ç¼€
    "'expect-contains:" ~ output_expectation
}

/// æœ‰å…³ã€Œä¿å­˜è¾“å‡ºã€çš„ã€Œé­”æ³•æ³¨é‡Šã€
/// âœ¨å­˜å‚¨ç¼“å­˜çš„æ‰€æœ‰è¾“å‡ºåˆ°æŒ‡å®šè·¯å¾„ä¸‹çš„æ–‡ä»¶ï¼ˆé˜»å¡ä¸»çº¿ç¨‹ï¼‰
comment_save_outputs = {
    // é¢å¤–çš„å‰ç¼€
    "'save-outputs:" ~ output_expectation
}

/// æœ‰å…³ã€Œç»ˆæ­¢ã€çš„ã€Œé­”æ³•æ³¨é‡Šã€
/// âœ¨ç»ˆæ­¢NAVMè™šæ‹Ÿæœº
/// * ğŸ“„å‚æ•°ï¼šé€‰é¡¹ã€ç†ç”±
/// * é€‰é¡¹ï¼šå†³å®šæ‰§è¡Œçš„æ¡ä»¶
/// * ğŸ“Œæ—  â‡’ æ— æ¡ä»¶å¼ºåˆ¶é€€å‡º
/// * ğŸ“Œæœ‰ â‡’ æœ‰æ¡ä»¶ï¼Œæˆ–å…¶å®ƒå‰¯ä½œç”¨
/// * ç†ç”±ï¼šå†³å®šè¿”å›æ˜¯å¦ã€Œæ­£å¸¸ã€
/// * ğŸ“Œæ— ç†ç”± â‡’ è™šæ‹Ÿæœºè¿”å› `Ok`
/// * ğŸ“Œæœ‰ç†ç”± â‡’ è™šæ‹Ÿæœºè¿”å› `Err(ç»ˆæ­¢ç†ç”±)`
comment_terminate = {
    // é¢å¤–çš„å‰ç¼€ | å¯é€‰çš„ã€Œé”™è¯¯ã€å‚æ•°
    "'terminate" ~ ("(" ~ comment_terminate_option ~ ")")? ~ (":" ~ comment_raw)?
}

/// è™šæ‹Ÿæœºç»ˆæ­¢æŒ‡ä»¤çš„é€‰é¡¹
/// * ğŸ¯æ§åˆ¶ç»ˆæ­¢çš„å‰ææ¡ä»¶ï¼šå¯ä»¥åœ¨ã€Œç»ˆæ­¢ã€åäº¤ç”±ç”¨æˆ·è¾“å…¥
comment_terminate_option = @{ "if-no-user" }

/// åŸå§‹æ³¨é‡Šè¯­æ³•ï¼šçº¯ç²¹çš„è¡Œæ³¨é‡Š
/// * âœ…`REM`çš„è¯­æ³•ç³–
comment_raw = @{ (!"\n" ~ ANY)* }

/// è¾“å‡ºé¢„æœŸ
/// * ğŸ“Œåªæè¿°ã€Œé¢„æœŸçš„å†…å®¹ã€ï¼Œä¸ã€Œå…·ä½“çš„ä½¿ç”¨æ–¹å¼ã€æ— å…³
/// * ğŸš©ã€2024-03-31 17:10:03ã€‘ç›®å‰ä¸åŒ…å«å¯¹ã€ŒåŸå§‹å†…å®¹ã€çš„é¢„æœŸï¼šå¹¶éè·¨CINé€šç”¨
output_expectation = {
    output_type? ~ narsese? ~ output_operation?
}

/// NAVMè¾“å‡ºçš„ã€Œç±»å‹ã€
/// * ğŸš©ç›´æ¥ä½¿ç”¨å†…å®¹
/// * ğŸ“åŸå­æ“ä½œé…åˆç©ºæ ¼è¯†åˆ«
output_type = @{ (!WHITE_SPACE ~ ANY)* }

/// NAVMè¾“å‡ºä¸­ã€Œæ“ä½œã€çš„ä¸€ç§è¡¨å¾å½¢å¼
/// * ğŸš©åˆ»æ„ä¸CommonNarseseè¯­æ³•ä¸ä¸€è‡´ï¼Œä»¥ä¾¿çœå»ã€ŒXX=ã€å‰ç¼€è¿›è¡Œè¯†åˆ«
output_operation = {
    "(" ~ "^" ~ atom_content ~ "," ~ term_list? ~ ")"
}

/// Narsese | ä¼˜å…ˆçº§ï¼šä»»åŠ¡ > è¯­å¥ > è¯é¡¹
/// * ğŸš©ä¸ä½¿ç”¨ã€Œé™é»˜è§„åˆ™ã€ï¼Œè®©å‰©ä¸‹çš„è¯­æ³•æ ‘ä½œä¸ºã€ŒNarseseè¾¹ç•ŒåŒ¹é…ã€ç”¨
/// * ğŸš©ä¸èƒ½ä½¿ç”¨ã€ŒåŸå­è§„åˆ™ã€åŒ¹é…æ•´ä¸ªå­—ç¬¦ä¸²ï¼šä¼šå¯¼è‡´åŒ¹é…å¤±è´¥
/// * âœ…`NSE`çš„è¯­æ³•ç³–
narsese = {
    task
  | sentence
  | term
}

/// ä»»åŠ¡ï¼šæœ‰é¢„ç®—çš„è¯­å¥
task = {
    budget ~ sentence
}

/// é¢„ç®—å€¼ | ä¸åŒ…æ‹¬ã€Œç©ºå­—ä¸²ã€éšå«çš„ã€Œç©ºé¢„ç®—ã€
budget = {
    "$" ~ budget_content ~ "$"
}

/// é¢„ç®—å€¼å†…å®¹
budget_content = _{
    (truth_budget_term ~ (";" ~ truth_budget_term)* ~ ";"*)
  | "" // ç©ºé¢„ç®—ï¼ˆä½†å¸¦æ‹¬å·ï¼‰
}

/// é€šç”¨äºçœŸå€¼ã€é¢„ç®—å€¼çš„é¡¹ | ç”¨ä½œå†…éƒ¨æ•°å€¼ï¼Œä¸çº¦æŸå–å€¼èŒƒå›´
truth_budget_term = @{ (ASCII_DIGIT | ".")+ }

/// è¯­å¥ = è¯é¡¹ æ ‡ç‚¹ æ—¶é—´æˆ³? çœŸå€¼?
sentence = {
    term ~ punctuation ~ stamp? ~ truth?
}

/// è¯é¡¹ = é™ˆè¿° | å¤åˆ | åŸå­
term = _{
    statement
  | compound
  | atom
}

/// é™ˆè¿° = <è¯é¡¹ ç³»è¯ è¯é¡¹>
statement = {
    "<" ~ term ~ copula ~ term ~ ">"
}

/// é™ˆè¿°ç³»è¯
copula = @{
    (punct_sym ~ "-" ~ punct_sym) // ç»§æ‰¿/ç›¸ä¼¼/å®ä¾‹/å±æ€§/å®ä¾‹å±æ€§

  | (punct_sym ~ "=" ~ punct_sym) // è•´å«/ç­‰ä»·

  | ("=" ~ punct_sym ~ ">") // æ—¶åºæ€§è•´å«

  | ("<" ~ punct_sym ~ ">") // æ—¶åºæ€§ç­‰ä»·
}

/// æ ‡ç‚¹ç¬¦å· | ç”¨äºã€ŒåŸå­è¯é¡¹å‰ç¼€ã€ã€Œå¤åˆè¯é¡¹è¿æ¥è¯ã€å’Œã€Œé™ˆè¿°ç³»è¯ã€
punct_sym = { (PUNCTUATION | SYMBOL) }

/// å¤åˆ = (è¿æ¥è¯, è¯é¡¹...) | {å¤–å»¶é›†...} | [å†…æ¶µé›†...]
compound = {
    compound_common
  | ext_set
  | int_set
}

/// é€šç”¨çš„å¤åˆè¯é¡¹
compound_common = { ("(" ~ connecter ~ "," ~ term_list ~ ")") }

/// é€šç”¨çš„ã€Œè¯é¡¹åˆ—è¡¨ã€ | é™é»˜å±•å¼€
term_list = _{ term ~ ("," ~ term)* }

/// å¤–å»¶é›† | ğŸ“Œã€2024-03-29 09:39:39ã€‘pestä»£ç æŠ˜å ä¸­ä¼šä¸¢æ‰æ‰€æœ‰ã€Œä¸è¢«è§„åˆ™æ•è·çš„å­—ç¬¦ä¸²ä¿¡æ¯ã€
ext_set = { "{" ~ term_list ~ "}" }

/// å†…æ¶µé›†
int_set = { "[" ~ term_list ~ "]" }

/// å¤åˆè¯é¡¹è¿æ¥è¯
connecter = @{ punct_sym ~ (!"," ~ punct_sym)* }

/// åŸå­ = å‰ç¼€ï¼ˆå¯é€‰ï¼‰ å†…å®¹
atom = {
    placeholder // å ä½ç¬¦

  | (atom_prefix ~ atom_content) // å˜é‡/é—´éš”/æ“ä½œâ€¦â€¦

  | atom_content // è¯è¯­
}

/// å ä½ç¬¦ = çº¯ä¸‹åˆ’çº¿å­—ç¬¦ä¸²
placeholder = @{ "_"+ }

/// åŸå­è¯é¡¹å‰ç¼€
atom_prefix = @{ (!"<" ~ !"(" ~ !"[" ~ !"{" ~ punct_sym)+ }

/// åŸå­è¯é¡¹å†…å®¹ | å·²é¿å…ä¸ã€Œå¤åˆè¯é¡¹ç³»è¯ã€ç›¸å†²çª
atom_content = @{ atom_char ~ (!copula ~ atom_char)* }

/// èƒ½ä½œä¸ºã€ŒåŸå­è¯é¡¹å†…å®¹ã€çš„å­—ç¬¦
atom_char = { LETTER | NUMBER | "_" | "-" }

/// æ ‡ç‚¹
punctuation = { (PUNCTUATION | SYMBOL) }

/// æ—¶é—´æˆ³ | ç©ºæ—¶é—´æˆ³ä¼šç›´æ¥åœ¨ã€Œè¯­å¥ã€ä¸­ç¼ºçœ
stamp = {
    ":" ~ (!":" ~ ANY)+ ~ ":"
}

/// çœŸå€¼ | ç©ºçœŸå€¼ä¼šç›´æ¥åœ¨ã€Œè¯­å¥ã€ä¸­ç¼ºçœ
truth = {
    "%" ~ (truth_budget_term ~ (";" ~ truth_budget_term)* ~ ";"*) ~ "%"
}
