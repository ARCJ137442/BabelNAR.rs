narsese = _{
    task
  | sentence
  | term
}

term = {
    compound
  | set
  | statement
  | atom
}

// åŸå­è¯é¡¹
// * ğŸ“å…¶ä¸­çš„`@`ä¼šåœ¨ã€Œä¸­é—´æœ‰ç©ºç™½ç¬¦ã€æ—¶æŠ¥é”™
atom         =  {
    placeholder // å ä½ç¬¦ | å¯èƒ½ä¼šå æ»¡

  | atom_prefix ~ atom_content
}
placeholder  =  { ("_"+) }
atom_prefix  = @{
    (PUNCTUATION | SYMBOL) // å…¶å®ƒå¸¦å‰ç¼€åŸå­è¯é¡¹

  | "" // è¯è¯­
}
atom_content = @{ word_content }

// å¤åˆè¯é¡¹

compound = ${
    // * ğŸ“ä½¿ç”¨å‰ç¼€ã€Œ$ã€å…è®¸åœ¨æ­¤ä¸­åˆ¤æ–­ç©ºæ ¼
    ("(" ~ #multi = connecter ~ optional_separator ~ term_list ~ ")")
  | ("(" ~ term ~ #binary = connecter ~ term ~ ")")
}

optional_separator = _{ (("," | WHITESPACE) ~ WHITESPACE*) }

// è¿æ¥ç¬¦ | ğŸ“ä½¿ç”¨` ~ (!"," ~ punct_sym)*`çš„æ¨¡å¼ï¼Œåœ¨æ¯æ¬¡ã€Œå°è¯•å»¶ä¼¸ã€æ—¶éƒ½åŒ¹é…
connecter = @{ punct_sym ~ (!"," ~ punct_sym)* }

// é›†åˆè¯é¡¹
set = {
    #ext_set = ("{" ~ term_list ~ "}") // å¤–å»¶é›†

  | #int_set = ("[" ~ term_list ~ "]") // å†…æ¶µé›†
}

term_list = { term ~ ((("," | WHITESPACE) ~ WHITESPACE*) ~ term)* ~ (",")? }

// é™ˆè¿°
statement = {
    "<" ~ term ~ copula ~ term ~ ">"
}

punct_sym = _{ (PUNCTUATION | SYMBOL) }

copula = @{
    // (PUNCTUATION | SYMBOL)+
    (punct_sym ~ "-" ~ punct_sym) // ç»§æ‰¿/ç›¸ä¼¼/å®ä¾‹/å±æ€§/å®ä¾‹å±æ€§

  | (punct_sym ~ "=" ~ punct_sym) // è•´å«/ç­‰ä»·

  | (punct_sym ~ punct_sym ~ ">") // æ—¶åºæ€§è•´å«/æ—¶åºæ€§ç­‰ä»·
}

// è¯­å¥

sentence = { term ~ punctuation ~ stamp ~ truth }

punctuation = @{ PUNCTUATION }

stamp         = @{
    (":" ~ stamp_content ~ ":") // æœ‰å†…å®¹çš„æ—¶é—´æˆ³

  | "" // ç©ºæ—¶é—´æˆ³
}
stamp_content = @{
    "|"
  | "\\"
  | "/"
  | (("+" | "-") ~ ASCII_DIGIT+)
}

truth              =  {
    ("%" ~ truth_budget_terms ~ "%") // æœ‰å€¼çš„çœŸå€¼

  | "" // ç©ºçœŸå€¼
}
truth_budget_term  = @{
    truth_budget_content+
}
truth_budget_terms =  {
    (truth_budget_term ~ (";" ~ truth_budget_term)*)
  | ""
}

// ä»»åŠ¡
task   = {
    budget ~ sentence
}
budget = {
    "$" ~ truth_budget_terms ~ "$"
}

// åŸºç¡€è®¾æ–½ //
// * ğŸ“ç”¨äºã€Œé˜²æ­¢è¯¯åƒç³»è¯ã€çš„æ–¹æ³•ï¼šå­—ç¬¦ ~ (è¦é¿å…çš„ ~ å­—ç¬¦)*
word_content         = { word_content_char ~ (!copula ~ word_content_char)* }
word_content_char    = { LETTER | NUMBER | "_" | "-" }
truth_budget_content = { ASCII_DIGIT | "." }

// å¿½ç•¥ç©ºç™½ç¬¦
WHITESPACE = _{ WHITE_SPACE }
