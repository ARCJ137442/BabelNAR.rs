//! ONAæ–¹è¨€è¯­æ³•
//! * ğŸ¯ä»ONAè¾“å‡ºä¸­è§£æNarsese
//! * ğŸ“Œå¤åˆè¯é¡¹çš„ã€Œä¸­ç¼€å½¢å¼ã€ã€Œç©ºæ ¼åˆ†éš”ã€å½¢å¼
//! * ğŸ“„ä»¥ç©ºæ ¼åˆ†éš”çš„è¯é¡¹ï¼š`(* {SELF})`
//! * ğŸ“„`({SELF} * x)`

/// ç©ºç™½ç¬¦ | æ‰€æœ‰Unicodeç©ºç™½ç¬¦ï¼Œè§£æå‰å¿½ç•¥
WHITESPACE = _{ WHITE_SPACE }

/// æ€»å…¥å£ï¼šè¯æ³•Narsese | ä¼˜å…ˆçº§ï¼šä»»åŠ¡ > è¯­å¥ > è¯é¡¹
narsese = _{
    task
  | sentence
  | term
}

/// ä»»åŠ¡ï¼šæœ‰é¢„ç®—çš„è¯­å¥
task = {
    budget ~ sentence
}

/// é¢„ç®—å€¼ | ä¸åŒ…æ‹¬ã€Œç©ºå­—ä¸²ã€éšå«çš„ã€Œç©ºé¢„ç®—ã€
budget = {
    "$" ~ budget_content ~ "$"
}

/// é¢„ç®—å€¼å†…å®¹
budget_content = _{
    (truth_budget_term ~ (";" ~ truth_budget_term)* ~ ";"*)
  | "" // ç©ºé¢„ç®—ï¼ˆä½†å¸¦æ‹¬å·ï¼‰
}

/// é€šç”¨äºçœŸå€¼ã€é¢„ç®—å€¼çš„é¡¹ | ç”¨ä½œå†…éƒ¨æ•°å€¼ï¼Œä¸çº¦æŸå–å€¼èŒƒå›´
truth_budget_term = @{ (ASCII_DIGIT | ".")+ }

/// è¯­å¥ = è¯é¡¹ æ ‡ç‚¹ æ—¶é—´æˆ³? çœŸå€¼?
sentence = {
    term ~ punctuation ~ stamp? ~ truth?
}

/// è¯é¡¹ = é™ˆè¿° | å¤åˆ | åŸå­
term = _{
    statement
  | compound
  | atom
}

/// é™ˆè¿° = <è¯é¡¹ ç³»è¯ è¯é¡¹> | é**åŸå­è§„åˆ™**ï¼Œå¼ºåˆ¶å…¶å†…è¡¨è¾¾å¼å¿½ç•¥ç©ºæ ¼
statement = !{
    "<" ~ term ~ copula ~ term ~ ">"
}

/// é™ˆè¿°ç³»è¯
copula = @{
    (punct_sym ~ "-" ~ punct_sym) // ç»§æ‰¿/ç›¸ä¼¼/å®ä¾‹/å±æ€§/å®ä¾‹å±æ€§

  | (punct_sym ~ "=" ~ punct_sym) // è•´å«/ç­‰ä»·

  | ("=" ~ punct_sym ~ ">") // æ—¶åºæ€§è•´å«

  | ("<" ~ punct_sym ~ ">") // æ—¶åºæ€§ç­‰ä»·
}

/// æ ‡ç‚¹ç¬¦å· | ç”¨äºã€ŒåŸå­è¯é¡¹å‰ç¼€ã€ã€Œå¤åˆè¯é¡¹è¿æ¥è¯ã€å’Œã€Œé™ˆè¿°ç³»è¯ã€
punct_sym = { (PUNCTUATION | SYMBOL) }

/// å¤åˆ = (è¿æ¥è¯, è¯é¡¹...) | {å¤–å»¶é›†...} | [å†…æ¶µé›†...]
/// * ğŸ†•å¯¹ONAå…¼å®¹å½¢å¦‚`(^op, {SELF}, LEFT)`çš„è¾“å‡ºè¯­æ³•
/// * ğŸš©æ­¤å¤„ä¸è¿›è¡Œã€Œé™é»˜å†…è”ã€ï¼šä¾¿äºåœ¨ã€ŒæŠ˜å å‡½æ•°ã€ä¸­å‘ä¸‹åˆ†æ´¾
/// * ğŸ“ä½¿ç”¨å‰ç¼€ã€Œ$ã€åœæ­¢å¿½ç•¥ç©ºæ ¼ï¼Œä»¥ä½¿ç”¨ã€Œå¯é€‰åˆ†éš”ç¬¦ã€
compound = !{
    compound_common
  | compound_binary
  | ext_set
  | int_set
}

/// é€šç”¨çš„å¤åˆè¯é¡¹
compound_common = ${ "(" ~ connecter ~ optional_separator ~ term_list ~ ")" }

/// é€šç”¨çš„ã€Œè¯é¡¹åˆ—è¡¨ã€ | é™é»˜å±•å¼€
term_list = _{ term ~ (optional_separator ~ term)* }

/// å¯é€‰çš„åˆ†éš”ç¬¦
optional_separator = _{
    ("," ~ WHITESPACE*)
  | WHITESPACE+
}

/// ğŸ†•ONAç‰¹å®šçš„ã€ŒäºŒå…ƒä¸­ç¼€è¡¨è¾¾æ³•ã€
/// * ğŸš©ã€2024-03-29 09:40:38ã€‘ç›®å‰é€šç”¨æˆ`(A, B, C)` => `<(*, B, C) --> A>`çš„è½¬æ¢æ–¹å¼
compound_binary = { "(" ~ term ~ connecter ~ term ~ ")" }

/// å¤–å»¶é›† | ğŸ“Œã€2024-03-29 09:39:39ã€‘pestä»£ç æŠ˜å ä¸­ä¼šä¸¢æ‰æ‰€æœ‰ã€Œä¸è¢«è§„åˆ™æ•è·çš„å­—ç¬¦ä¸²ä¿¡æ¯ã€
ext_set = { "{" ~ term_list ~ "}" }

/// å†…æ¶µé›†
int_set = { "[" ~ term_list ~ "]" }

/// å¤åˆè¯é¡¹è¿æ¥è¯ | âš ï¸ä¸åŒ…æ‹¬ã€Œé€—å·ã€ä¸ã€Œåœ†æ‹¬å·ã€
connecter = @{ (!"," ~ !"(" ~ !")" ~ punct_sym)* }

/// åŸå­ = å‰ç¼€ï¼ˆå¯é€‰ï¼‰ å†…å®¹
atom = {
    placeholder // å ä½ç¬¦

  | (atom_prefix ~ atom_content) // å˜é‡/é—´éš”/æ“ä½œâ€¦â€¦

  | atom_content // è¯è¯­
}

/// å ä½ç¬¦ = çº¯ä¸‹åˆ’çº¿å­—ç¬¦ä¸²
placeholder = @{ "_"+ }

/// åŸå­è¯é¡¹å‰ç¼€
atom_prefix = @{ punct_sym+ }

/// åŸå­è¯é¡¹å†…å®¹ | å·²é¿å…ä¸ã€Œå¤åˆè¯é¡¹ç³»è¯ã€ç›¸å†²çª
atom_content = @{ atom_char ~ (!copula ~ atom_char)* }

/// èƒ½ä½œä¸ºã€ŒåŸå­è¯é¡¹å†…å®¹ã€çš„å­—ç¬¦
atom_char = { LETTER | NUMBER | "_" | "-" }

/// æ ‡ç‚¹
punctuation = { (PUNCTUATION | SYMBOL) }

/// æ—¶é—´æˆ³ | ç©ºæ—¶é—´æˆ³ä¼šç›´æ¥åœ¨ã€Œè¯­å¥ã€ä¸­ç¼ºçœ
stamp = {
    ":" ~ (!":" ~ ANY)+ ~ ":"
}

/// çœŸå€¼ | ç©ºçœŸå€¼ä¼šç›´æ¥åœ¨ã€Œè¯­å¥ã€ä¸­ç¼ºçœ
truth = {
    "%" ~ (truth_budget_term ~ (";" ~ truth_budget_term)* ~ ";"*) ~ "%"
}
